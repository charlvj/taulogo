to :christmas_scene []
[ draw_animated [
    relocate -100 -200

    snowman

    relocate -340 -300

    tree 5 80 300

    relocate 80 500

    tree 3 40 150

    relocate 50 200

    tree 5 50 220
 
    relocate -130 250

    tree 5 60 300
  ]

  relocate 0 -1000
  repeat 50 
  [ make :r_up random_int 600
    make :r_right random_int 1200
    make :a random_int 60
    relocate r_up r_right
    rt a
    draw_animated [ snowflake random_int 10 ]
    lt a
    relocate minus 0 r_up minus 0 r_right
  ]


  penup fd 340 rt 90 fd 349 pendown lt 90

  wait 100
  animate_the_hat
  animate_the_hat
]
expose :christmas_scene

to :animate_the_hat []
[ repeat 5
  [ setforeground white snowman_hat 80
    fd 5 rt 5
    setforeground black snowman_hat 80
    wait 10
  ]
  repeat 5
  [ setforeground white snowman_hat 80
    lt 5 bk 5
    setforeground black snowman_hat 80
    wait 10
  ]
]

to :circle [:step :angle :direction]
[ make :count div 360 angle
  repeat count 
  [ fd step 
    call direction angle 
  ]
]


to :snowman []
[ rt 90
  /* Bottom circle */
  circle 15 10 @rt
  /* Middle circle */
  circle 10 10 @lt
  repeat 18 [ fd 10 lt 10 ]
  /* The Head */
  circle 8 10 @rt
  
  /* go to the left and draw an arm */
  repeat 9 [ fd 10 lt 10 ]
  rt 90
  snowman_arm 80

  /* Now do the other side */
  rt 90
  repeat 18 [ rt 10 fd 10 ] bk 10
  lt 90
  snowman_arm 80

  /* Get back to the bottom of the torso */
  rt 90
  fd 10 repeat 9 [ rt 10 fd 10 ] bk 5

  /* Make some buttons */
  rt 100 penup fd 25 pendown circle 1 20 @rt
  lt 5 penup fd 25 pendown circle 1 20 @rt
  lt 5 penup fd 25 pendown circle 1 20 @rt
  lt 5 penup fd 25 pendown circle 1 20 @rt
  
  /* Get to the center base of the head */
  penup fd 15
  /* up a little */
  rt 5 fd 15
  /* Get into pos for a mouth */
  lt 90   repeat 9 [ fd 2 rt 10 ]
  rt 120
  /* And draw a mouth */
  pendown repeat 6 [ fd 4 lt 10 ]

  /* Move to the nose */
  penup repeat 3 [ rt 10 bk 4 ]
  lt 90 fd 10

  /* draw nose */
  pendown
  rt 75 fd 20 lt 150 fd 20 rt 75

  /* Move to the eyes */
  penup fd 10

  /* Draw the eyes */
  lt 90 fd 10
  pendown
  rt 90 circle 1 20 @rt
  penup rt 90 fd 20 pendown
  lt 90 circle 1 20 @rt

  penup lt 90 fd 10 rt 90 fd 35 pendown
  snowman_hat 80
]


to :snowman_arm [:length]
[ make :finger_length div length 3
  fd length
  lt 20 fd finger_length bk finger_length
  rt 40 fd finger_length bk finger_length
  lt 20 fd finger_length bk finger_length
  bk length
]


to :filled_rect [:width :height]
[ make :half mult width 0.5
  
  lt 90 fd half rt 180

  repeat height [
    fd width lt 90 fd 1 lt 90 fd width rt 90 rt 90
  ]

  fd half lt 90
]


to :snowman_hat [:width]
[ make :half mult width 0.5
  filled_rect width 10
  filled_rect half 10
  bk 20
]


to :tree [:segments :seg_height :bottom_width]
[ make :r toRadians 50

  make :box_width div bottom_width 6
  make :box_width_half div box_width 2

  make :width_of_top_triangle   
    mult 
        2
        div 
          seg_height
          tan r 

  make :reduction_length
    mult 
      inverse  dec segments 
      minus bottom_width width_of_top_triangle
      
  lt 90 fd box_width_half
  rt 90 fd box_width
  rt 90 fd box_width
  rt 90 fd box_width
  rt 90 fd box_width_half
  rt 90

  penup fd box_width pendown

  repeat segments [
    make :width minus bottom_width mult $0 reduction_length
    iso_trap width seg_height 50
    penup fd seg_height pendown
  ]

  penup
  bk mult segments seg_height
  bk box_width
  pendown
]


to :iso_trap [:width :height :angle]
[ make :r toRadians angle
  make :center mult 0.5 width
  make :side_length 
  	div height 
  	    sin r
  make :top_length
    mult
      2
      minus
        center
        div height
             tan r 
  lt 90 fd center
  rt minus 180 angle
  fd side_length
  rt angle
  fd top_length
  rt angle
  fd side_length
  rt minus 180 angle
  fd center rt 90
]


to :iso_triangle [:width :height :angle]
[ make :r toRadians angle
  make :center div width 2
  make :side_length 
  	div height 
  	    sin r
  lt 90 fd center
  rt minus 180 angle
  fd side_length
  rt angle
  rt angle
  fd side_length
  rt minus 180 angle
  fd center rt 90
]


to :snowflake [:radius]
[  repeat 6 [ fd radius bk radius lt 60 ]
]


to :relocate [:up :right]
[ penup
  fd up rt 90 fd right lt 90
  pendown
]

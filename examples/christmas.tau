to :christmas_scene []
[ penup 
  bk 100
  rt 90 fd 200 lt 90
  pendown
  
  snowman

  penup bk 340 
  lt 90 fd 800 rt 90
  pendown

  tree 5 80 300

  penup
  rt 90 fd 230 lt 90 fd 80
  pendown

  tree 4 50 200
]
expose :christmas_scene

to :circle [:step :angle :direction]
[ make :count div 360 angle
  repeat count 
  [ fd step 
    call direction angle 
  ]
]
expose :circle

to :snowman []
[ rt 90
  /* Bottom circle */
  circle 15 10 @rt
  /* Middle circle */
  circle 10 10 @lt
  repeat 18 [ fd 10 lt 10 ]
  /* The Head */
  circle 8 10 @rt
  
  /* go to the left and draw an arm */
  repeat 9 [ fd 10 lt 10 ]
  rt 90
  snowman_arm 80

  /* Now do the other side */
  rt 90
  repeat 18 [ rt 10 fd 10 ] bk 10
  lt 90
  snowman_arm 80

  /* Get back to the bottom of the torso */
  rt 90
  fd 10 repeat 9 [ rt 10 fd 10 ] bk 5

  /* Make some buttons */
  rt 100 penup fd 25 pendown circle 1 20 @rt
  lt 5 penup fd 25 pendown circle 1 20 @rt
  lt 5 penup fd 25 pendown circle 1 20 @rt
  lt 5 penup fd 25 pendown circle 1 20 @rt
  
  /* Get to the center base of the head */
  penup fd 15
  /* up a little */
  rt 5 fd 15
  /* Get into pos for a mouth */
  lt 90   repeat 9 [ fd 2 rt 10 ]
  rt 120
  /* And draw a mouth */
  pendown repeat 6 [ fd 4 lt 10 ]

  /* Move to the nose */
  penup repeat 3 [ rt 10 bk 4 ]
  lt 90 fd 10

  /* draw nose */
  pendown
  rt 75 fd 20 lt 150 fd 20 rt 75

  /* Move to the eyes */
  penup fd 10

  /* Draw the eyes */
  lt 90 fd 10
  pendown
  rt 90 circle 1 20 @rt
  penup rt 90 fd 20 pendown
  lt 90 circle 1 20 @rt
]
expose :snowman


to :snowman_arm [:length]
[ make :finger_length div length 3
  fd length
  lt 20 fd finger_length bk finger_length
  rt 40 fd finger_length bk finger_length
  lt 20 fd finger_length bk finger_length
  bk length
]
expose :snowman_arm


to :tree [:segments :seg_height :bottom_width]
[ make :r toRadians 50

  make :box_width div bottom_width 6
  make :box_width_half mult box_width 0.5

  make :width_of_top_triangle   
    mult 
        2
        div 
          seg_height
          tan r 

  make :reduction_length
    mult 
      inverse  dec segments 
      minus bottom_width width_of_top_triangle
      
  lt 90 fd box_width_half
  rt 90 fd box_width
  rt 90 fd box_width
  rt 90 fd box_width
  rt 90 fd box_width_half
  rt 90

  penup fd box_width pendown

  repeat segments [
    make :width minus bottom_width mult $0 reduction_length
    printline [ $0 " - " width ]
    iso_trap width seg_height 50
    penup fd seg_height pendown
  ]

  penup
  bk mult segments seg_height
  bk box_width
  pendown
]
expose :tree


to :iso_trap [:width :height :angle]
[ make :r toRadians angle
  make :center mult 0.5 width
  make :side_length 
  	div height 
  	    sin r
  make :top_length
    mult
      2
      minus
        center
        div height
             tan r 
  lt 90 fd center
  rt minus 180 angle
  fd side_length
  rt angle
  fd top_length
  rt angle
  fd side_length
  rt minus 180 angle
  fd center rt 90
]
expose :iso_trap


to :iso_triangle [:width :height :angle]
[ make :r toRadians angle
  make :center div width 2
  make :side_length 
  	div height 
  	    sin r
  printline ["Triangle: " width " " height " " angle]
  printline ["  Center: " center "; sidelength: " side_length]
  lt 90 fd center
  rt minus 180 angle
  fd side_length
  rt angle
  rt angle
  fd side_length
  rt minus 180 angle
  fd center rt 90
]
expose :iso_triangle